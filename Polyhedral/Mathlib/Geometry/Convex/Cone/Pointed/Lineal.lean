
import Mathlib.Algebra.Module.Submodule.Pointwise
import Mathlib.Algebra.Order.Nonneg.Module
import Mathlib.Geometry.Convex.Cone.Basic

import Polyhedral.Mathlib.Geometry.Convex.Cone.Pointed.Basic

namespace PointedCone

open Module
open Submodule (span)

-- ## LINEAL

section LinearOrderedRing

open Pointwise

variable {R M : Type*} [Ring R] [LinearOrder R] [IsOrderedRing R] [AddCommGroup M] [Module R M]

/-- Every submodule contain int he cone is also contained in the lineality space. -/
lemma le_lineal {C : PointedCone R M} {S : Submodule R M} (hS : S ≤ C) :
    S ≤ C.lineal := by simp only [lineal_eq_sSup]; exact le_sSup hS

lemma neg_mem_of_mem_lineal {C : PointedCone R M} {x : M} (hx : x ∈ C.lineal) : -x ∈ C := by
  rw [← Submodule.neg_mem_iff] at hx
  exact lineal_le C hx

lemma mem_of_neg_mem_lineal {C : PointedCone R M} {x : M} (hx : -x ∈ C.lineal) : x ∈ C := by
  rw [Submodule.neg_mem_iff] at hx
  exact lineal_le C hx

def lineal_inf_neg (C : PointedCone R M) : C.lineal = C ⊓ -C := by
  ext x; simp

def lineal_mem_neg (C : PointedCone R M) : C.lineal = {x ∈ C | -x ∈ C} := by
  ext x; simpa using mem_lineal

@[simp]
lemma lineal_inf (C D : PointedCone R M) : (C ⊓ D).lineal = C.lineal ⊓ D.lineal := by
  ext x; simp [mem_lineal]; aesop

@[simp] lemma submodule_lineal (S : Submodule R M) : (S : PointedCone R M).lineal = S := by
  ext x; simp [mem_lineal]

@[simp] lemma lineal_top : (⊤ : PointedCone R M).lineal = ⊤ := submodule_lineal ⊤

@[simp] lemma lineal_bot : (⊥ : PointedCone R M).lineal = ⊥ := submodule_lineal ⊥

lemma lineal_mono {C D : PointedCone R M} (h : C ≤ D) : C.lineal ≤ D.lineal := by
  intro x hx
  rw [mem_lineal] at *
  exact ⟨h hx.1, h hx.2⟩

/- In this section we show properties of lineal that also follow from lineal
  being a face. But we need this earlier than faces, so we need to prove that
  lineal is a face here. This can then be resused later.

  Alternatively, lineal can be defined in Faces.lean
-/

lemma lineal_isExtreme_left {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    (hxy : x + y ∈ C.lineal) : x ∈ C.lineal := by
  have hxy' := neg_mem_of_mem_lineal hxy
  have hx' := C.add_mem hy hxy'
  simp only [neg_add_rev, add_neg_cancel_left] at hx'
  exact mem_lineal.mpr ⟨hx, hx'⟩

lemma lineal_isExtreme_right {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    (hxy : x + y ∈ C.lineal) : y ∈ C.lineal := by
  rw [add_comm] at hxy; exact lineal_isExtreme_left hy hx hxy

lemma lineal_isExtreme {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    (hxy : x + y ∈ C.lineal) : x ∈ C.lineal ∧ y ∈ C.lineal :=
  ⟨lineal_isExtreme_left hx hy hxy, lineal_isExtreme_right hx hy hxy⟩

lemma lineal_isExtreme_right_of_inv {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    {c : R} (hc : 0 < c) (hc' : Invertible c) (hxy : x + c • y ∈ C.lineal) : y ∈ C.lineal := by
  have h := lineal_isExtreme_right hx (C.smul_mem (le_of_lt hc) hy) hxy
  simpa using C.lineal.smul_mem (Invertible.invOf c) h

lemma lineal_isExtreme_left_of_inv {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    {c : R} (hc : 0 < c) (hc' : Invertible c) (hxy : c • x + y ∈ C.lineal) : x ∈ C.lineal := by
  have h := lineal_isExtreme_left (C.smul_mem (le_of_lt hc) hx) hy hxy
  simpa using C.lineal.smul_mem (Invertible.invOf c) h

lemma lineal_isExtreme_sum {C : PointedCone R M} {xs : Finset M} (hxs : (xs : Set M) ⊆ C)
    (h : ∑ x ∈ xs, x ∈ C.lineal) : (xs : Set M) ⊆ C.lineal := by classical
  induction xs using Finset.induction_on with
  | empty => simp
  | insert y xs hy H =>
    simp only [Set.subset_def, Finset.mem_coe, SetLike.mem_coe, Finset.coe_insert,
      Set.mem_insert_iff, forall_eq_or_imp, Finset.sum_insert hy] at *
    have h := lineal_isExtreme hxs.1 (C.sum_mem hxs.2) h
    exact ⟨h.1, H hxs.2 h.2⟩

@[simp] lemma sup_lineal_eq (C : PointedCone R M) : C ⊔ C.lineal = C :=
    sup_of_le_left (lineal_le C)

-- NOTE: equality holds, e.g., if D is a face of C
lemma lineal_sup_le (C D : PointedCone R M) : C.lineal ⊔ D.lineal ≤ (C ⊔ D).lineal := by
  intro x
  simp only [Submodule.mem_sup, mem_lineal, forall_exists_index, and_imp]
  intro y hy hy' z hz hz' rfl
  exact ⟨⟨y, hy, by use z⟩, -y, hy', -z, hz', by simp [add_comm]⟩

-- ## PRIORITY
--isnt this false when C and D are two rays pointing in opposite directions?
lemma _lineal_sup_eq (C D : PointedCone R M) (hCD : span R C ⊓ D.lineal ≤ C.lineal) :
    (C ⊔ D).lineal = C.lineal ⊔ D.lineal := by
  rw [le_antisymm_iff, and_comm]
  constructor
  · exact lineal_sup_le ..
  intro x
  simp [Submodule.mem_sup, mem_lineal]
  simp [SetLike.le_def, mem_lineal] at hCD
  intro y hy z hz hyz w hw v hv hwv
  have h := hCD
  sorry

-- !! only holds over fields or archimedean rings! Not in general.
lemma mem_lineal_of_smul_mem_lineal' {C : PointedCone R M} :
  (∀ c > 0, ∃ d > 0, d * c ≥ 1) ↔ (∀ x ∈ C, ∀ c > 0, c • x ∈ C.lineal → x ∈ C.lineal) := sorry

-- !! only holds over fields or archimedean rings! Not in general.
lemma mem_lineal_of_smul_mem_lineal {C : PointedCone R M} {x : M} {c : R}
    (hx : x ∈ C) (hcx : c • x ∈ C.lineal) : x ∈ C.lineal := by
  wlog h0c : 0 ≤ c
  · sorry
  · wlog h1c : 1 ≤ c with H
    · --have H := @H _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ (1+1) hx
      sorry
    · have h : c = c - 1 + 1 := by simp
      rw [h] at hcx
      clear h
      rw [add_smul] at hcx
      simp at hcx
      have h' : 1 ≤ c ↔ 0 ≤ c - 1 := by simp
      rw [h'] at h1c
      replace h' := smul_mem C h1c hx
      exact lineal_isExtreme_right h' hx sorry

-- TODO: maybe this result is not really necessary. See `inf_sup_eq_self_of_le_of_codisjoint` below.
/-- If `C` is a cone and `S` is complementary to the cone's linealiry space, then `C` can
  be written as `(C ⊓ S) ⊔ C.lineal`. -/
lemma inf_sup_lineal {C : PointedCone R M} {S : Submodule R M} (hCS : Codisjoint C.lineal S) :
    (C ⊓ S) ⊔ C.lineal = C := by
  sorry

lemma inf_sup_eq_self_of_le_of_codisjoint {C : PointedCone R M} {S : PointedCone R M}
    {T : Submodule R M} (hT : T ≤ C) (hST : Codisjoint S T) : (C ⊓ S) ⊔ T = C := by
  simp [inf_sup_assoc_of_le_of_submodule_le _ hT, hST.eq_top]

lemma lineal_le_span (C : PointedCone R M) : C.lineal ≤ span R C := by
  rw [← ofSubmodule_mono]
  exact le_trans (lineal_le C) Submodule.subset_span

/-- The linear span of `C ⊓ -C` is the lineality space of `C`. -/
lemma span_inf_neg_eq_lineal (C : PointedCone R M) : span R (C ⊓ -C) = C.lineal := by
  simpa [coe_lineal] using (Submodule.span_eq C.lineal)


-- ## MAP

open Function LinearMap

variable {N : Type*} [AddCommGroup N] [Module R N]

lemma map_lineal_le (C : PointedCone R M) (f : M →ₗ[R] N) :
    C.lineal.map f ≤ (C.map f).lineal := by
  intro y
  simp only [Submodule.mem_map, mem_lineal, mem_map, forall_exists_index, and_imp]
  intro x hx hmx hfxy
  exact ⟨⟨x, hx, hfxy⟩, ⟨-x, hmx, by rw [← hfxy, LinearMap.map_neg]⟩⟩

lemma map_lineal (C : PointedCone R M) {f : M →ₗ[R] N} (hf : Injective f) :
    (C.map f).lineal = C.lineal.map f := by
  rw [le_antisymm_iff]
  constructor
  · intro x
    simp only [mem_lineal, mem_map, Submodule.mem_map, and_imp, forall_exists_index]
    intro y hy hfxy z hz hfxz
    use y
    · constructor
      · constructor
        · exact hy
        rw [← hfxy, ← LinearMap.map_neg] at hfxz
        simpa [← hf hfxz] using hz
      · exact hfxy
  · exact map_lineal_le C f

lemma comap_lineal (C : PointedCone R M) {f : N →ₗ[R] M} :
    (C.comap f).lineal = C.lineal.comap f := by
  ext x; simp [mem_lineal]

@[simp] lemma neg_lineal (C : PointedCone R M) : (-C).lineal = C.lineal := by
  simp [← comap_id_eq_neg, comap_lineal]

lemma lineal_restrict (S : Submodule R M) (C : PointedCone R M) :
    (restrict S C).lineal = .restrict S C.lineal := by
  simp only [Submodule.submoduleOf, ← comap_lineal, comap]
  congr

lemma lineal_embed (S : Submodule R M) (C : PointedCone R S) :
    (embed C).lineal = .embed C.lineal := by simp [map_lineal]

end LinearOrderedRing


section DivisionRing

variable {R : Type*} [DivisionRing R] [LinearOrder R] [IsOrderedRing R]
variable {M : Type*} [AddCommGroup M] [Module R M]

lemma lineal_isExtreme_left' {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    {c : R} (hc : 0 < c) (hxy : c • x + y ∈ C.lineal) : x ∈ C.lineal := by
  exact lineal_isExtreme_left_of_inv hx hy hc (invertibleOfNonzero <| ne_of_gt hc) hxy

lemma lineal_isExtreme_right' {C : PointedCone R M} {x y : M} (hx : x ∈ C) (hy : y ∈ C)
    {c : R} (hc : 0 < c) (hxy : x + c • y ∈ C.lineal) : y ∈ C.lineal := by
  exact lineal_isExtreme_right_of_inv hx hy hc (invertibleOfNonzero <| ne_of_gt hc) hxy

lemma lineal_isExtreme_sum' {C : PointedCone R M} {xs : Finset M} (hxs : (xs : Set M) ⊆ C)
    (c : M → R) (hc : ∀ x ∈ xs, 0 < c x) (h : ∑ x ∈ xs, c x • x ∈ C.lineal) :
    ∀ x ∈ xs, c x ≠ 0 → x ∈ C.lineal := by classical
  induction xs using Finset.induction_on with
  | empty => simp
  | insert y xs hy H =>
    simp only [Set.subset_def, SetLike.mem_coe, ne_eq, Finset.coe_insert,
      Set.mem_insert_iff, forall_eq_or_imp, Finset.mem_insert, Finset.sum_insert hy] at *
    have hxsC := C.sum_mem (fun x hx => C.smul_mem (le_of_lt <| hc.2 x hx) (hxs.2 x hx))
    constructor
    · exact fun _ => lineal_isExtreme_left' hxs.1 hxsC hc.1 h
    · exact H hxs.2 hc.2 <| lineal_isExtreme_right (C.smul_mem (le_of_lt hc.1) hxs.1) hxsC h

variable (R) in
lemma hull_inter_lineal_eq_lineal (s : Set M) :
    hull R (s ∩ (hull R s).lineal) = (hull R s).lineal := by
  rw [le_antisymm_iff]
  constructor
  · rw [← Submodule.span_eq <| ofSubmodule ((hull R s).lineal)]
    refine Submodule.span_mono ?_
    simp only [Submodule.coe_restrictScalars, Set.inter_subset_right]
  · sorry
  -- constructor
  -- · rw [← Submodule.span_eq (C.lineal : PointedCone R M)]
  --   exact Submodule.span_mono (by simp)
  -- · rw [← SetLike.coe_subset_coe]
  --   rw [Set.subset_def]
  --   intro x hx
  --   let S:= s ∩ C.lineal
  --   let S' := s \ C.lineal
  --   have hS : S ∪ S' = s := by simp [S, S']
  --   have hS' : S ∩ S' = ∅ := by aesop

  --   --have hs : s = (s ∩ C.lineal) ∪ ()
  --   -- rw [Submodule.mem_span_finite_of_mem_span] at h
    -- sorry

end DivisionRing

section Ring

-- ## SALIENT

variable {R : Type*} [Ring R] [PartialOrder R] [IsOrderedRing R]
variable {M : Type*} [AddCommGroup M] [Module R M]

-- TODO: definition should probably be formulated without negation: x ∈ C → x = 0
/-- A salient cone has trivial lineality space, see `salient_iff_lineal_bot`. -/
abbrev Salient (C : PointedCone R M) := C.toConvexCone.Salient

@[simp] lemma bot_salient : (⊥ : PointedCone R M).Salient := by
  simp [Salient, ConvexCone.Salient]

lemma salient_iff_mem_neg {C : PointedCone R M} : C.Salient ↔ ∀ x ∈ C, x ≠ 0 → -x ∉ C := by rfl

lemma Salient.mem_neg_mem_zero {C : PointedCone R M} (hC : C.Salient)
    {x : M} (hx : x ∈ C) (hx' : -x ∈ C) : x = 0 := by
  specialize hC x hx
  rw [not_imp_not] at hC
  exact hC hx'

-- TODO: move to right place
section Nonneg

omit [IsOrderedRing R] in
@[simp] lemma _root_.Nonneg.coe_eq_zero (a : {c : R // 0 ≤ c}) : (a : R) = 0 ↔ a = 0 := by
  rw [Nonneg.mk_eq_zero]

end Nonneg

lemma salient_of_pos_linearMap {C : PointedCone R M} {f : M →ₗ[R] R}
    (h : ∀ c ∈ C, c ≠ 0 → 0 < f c) : C.Salient := by
  intro x hx hx0
  by_contra h'
  have hfnx := h (-x) h' (neg_ne_zero.mpr hx0)
  simp only [_root_.map_neg, Left.neg_pos_iff] at hfnx
  exact (lt_asymm hfnx) (h x hx hx0)

set_option backward.isDefEq.respectTransparency false in
lemma salient_hull_of_linearIndepOn {s : Set M} (h : LinearIndepOn R id s) :
    (hull R s).Salient := by classical
  rw [salient_iff_mem_neg]
  intro x hxp hx0 hxn
  absurd hx0
  rw [Submodule.mem_span_iff_exists_finset_subset] at hxp hxn
  obtain ⟨fp, tp, htsp, hftp, rfl⟩ := hxp
  obtain ⟨fn, tn, htsn, hftn, hsum⟩ := hxn
  let t := tp ∪ tn
  let f := fun x => fp x + fn x
  refine Finset.sum_eq_zero (fun x hx => ?_)
  have hlin := linearIndepOn_iff'.mp h t (f ·) (by simp [t, htsp, htsn])
  simp only [id_eq, Nonneg.coe_smul] at hlin
  specialize hlin ?_ x (Finset.subset_union_left hx)
  · simp only [f, add_smul, Finset.sum_add_distrib]
    have hsum1 : ∑ x ∈ t, fp x • x = ∑ x ∈ tp, fp x • x := by -- restrict t to tp
      refine Finset.sum_union_eq_left fun _ _ h ↦ ?_
      simp [fp.notMem_support.mp fun h2 ↦ h <| hftp h2]
    have hsum2 : ∑ x ∈ t, fn x • x = ∑ x ∈ tn, fn x • x := by -- restrict t to tn
      refine Finset.sum_union_eq_right fun _ _ h ↦ ?_
      simp [fn.notMem_support.mp fun h2 ↦ h <| hftn h2]
    rw [hsum1, hsum2, hsum, add_neg_cancel]
  rw [Nonneg.coe_eq_zero, add_eq_zero_iff_of_nonneg (zero_le _) (zero_le _)] at hlin
  simp only [hlin, zero_smul]

section IsDomain

variable [IsDomain R] [IsTorsionFree R M]

lemma salient_hull_singleton (x : M) : (hull R {x}).Salient := by
  by_cases h : x = 0
  · simp [h]
  · exact salient_hull_of_linearIndepOn (by simp [h])

-- NOTE: there is alos `ofSubmodule_salient_iff_eq_bot` below, which proven something stronger
--  for general rings, BUT assumes linear order. Is one setting better than the other?
lemma top_not_salient (h : Module.rank R M ≠ 0) : ¬(⊤ : PointedCone R M).Salient := by
  simpa [Salient, ConvexCone.Salient, rank_zero_iff_forall_zero] using h

end IsDomain

end Ring

section LinearOrderedRing

variable {R : Type*} [Ring R] [LinearOrder R] [IsOrderedRing R]
variable {M : Type*} [AddCommGroup M] [Module R M]

lemma Salient.of_le_salient {C D : PointedCone R M} (hC : C.Salient) (hD : D ≤ C) : D.Salient := by
  rw [Salient, ConvexCone.salient_iff_not_flat] at *
  by_contra h
  apply hC
  rcases h with ⟨x, xD, x_ne_0, neg_xD⟩
  exact ⟨x, hD xD, x_ne_0, hD neg_xD⟩
  -- have h' := h.mono hD

lemma salient_iff_lineal_bot {C : PointedCone R M} : C.Salient ↔ C.lineal = ⊥ := by
  constructor <;> intro h
  · ext x
    simp only [mem_lineal, Submodule.mem_bot]
    exact ⟨fun H => h.mem_neg_mem_zero H.1 H.2, by simp +contextual⟩
  · intro x hx
    rw [not_imp_not]
    intro hnx
    have hlin := mem_lineal.mpr ⟨hx, hnx⟩
    rw [h] at hlin
    exact hlin

@[simp] lemma ofSubmodule_salient_iff_eq_bot {S : Submodule R M} :
    (S : PointedCone R M).Salient ↔ S = ⊥ := by
  nth_rw 2 [← submodule_lineal S]
  exact salient_iff_lineal_bot

/-- If `S` is a submodule disjoint to the lineality space of a cone `C`, then `C ⊓ S`
  is salient. -/
lemma inf_salient {C : PointedCone R M} {S : Submodule R M} (hCS : Disjoint C.lineal S) :
    (C ⊓ S).Salient := by
  simp only [salient_iff_lineal_bot, lineal_inf, submodule_lineal, ← disjoint_iff, hCS]

-- ## MAP

open Function

variable {N : Type*} [AddCommGroup N] [Module R N]

omit [LinearOrder R] [IsOrderedRing R] in
-- TODO: generalize and move to the right place
@[simp] lemma injective_neg {f : N →ₗ[R] M} : Injective (-f) ↔ Injective f := by
  simp [Injective]

omit [LinearOrder R] [IsOrderedRing R] in
@[simp] lemma surjective_neg {f : N →ₗ[R] M} : Surjective (-f) ↔ Surjective f := by
  constructor
  · exact fun h x => by simpa using h (-x)
  · intro h x
    obtain ⟨y, hy⟩ := h (-x)
    exact ⟨y, by simp [hy]⟩

lemma salient_map {C : PointedCone R M} {f : M →ₗ[R] N} (hC : C.Salient) (hf : Injective f) :
    (C.map f).Salient := by
  rw [salient_iff_lineal_bot] at *
  simp [map_lineal _ hf, hC]

lemma salient_comap {C : PointedCone R M} {f : N →ₗ[R] M} (hC : C.Salient) (hf : Injective f) :
    (C.comap f).Salient := by
  rw [salient_iff_lineal_bot] at *
  simpa [comap_lineal, hC] using LinearMap.ker_eq_bot_of_injective hf

open Pointwise in
lemma salient_neg {C : PointedCone R M} (hC : C.Salient) : (-C).Salient := by
  simpa [← map_id_eq_neg] using salient_map hC (injective_neg.mpr injective_id)

-- ## SALIENT QUOT

variable {C : PointedCone R M}

/-- The quotient of a cone by its lineality space. -/
abbrev salientQuot (C : PointedCone R M) := C.quot C.lineal

lemma salientQuot_def (C : PointedCone R M) : C.salientQuot = C.quot C.lineal := rfl

lemma salient_salientQuot (C : PointedCone R M) : Salient C.salientQuot := by
  rw [Salient, ConvexCone.salient_iff_not_flat]
  intro h
  rcases h with ⟨x, hx, x_ne_0, hx'⟩
  rcases (Set.mem_image (⇑C.lineal.mkQ) (↑C) x).mp hx with ⟨y,yC, hy⟩
  rcases (Set.mem_image (⇑C.lineal.mkQ) (↑C) (-x)).mp hx' with ⟨y',yC', hy'⟩
  have : y ∉ C.lineal := by
    intro h
    apply x_ne_0
    rw [← hy]
    exact (Submodule.Quotient.mk_eq_zero C.lineal).mpr h
  apply this
  have : (C.lineal).mkQ (y+y') = 0 := by
    rw [map_add, hy, hy', add_neg_cancel]
  have sum_lineal : y+y' ∈ C.lineal := by
    rw [← Submodule.ker_mkQ C.lineal]
    exact LinearMap.mem_ker.mpr this
  apply mem_lineal.mp at sum_lineal
  have : -y ∈ C := by
    have : y' + -(y + y') = -y := by
      simp
    rw [← this]
    exact Submodule.add_mem C yC' (sum_lineal.2)
  exact mem_lineal.mpr ⟨yC, this⟩

@[simp] lemma salientQuot_of_submodule (S : Submodule R M) :
    (S : PointedCone R M).salientQuot = ⊥ := by
  unfold salientQuot
  rw [submodule_lineal, ← Submodule.span_eq S]
  simp only [Submodule.span_coe_eq_restrictScalars, Submodule.restrictScalars_self]
  rw [← coe_ofSubmodule, quot_span]

end LinearOrderedRing

end PointedCone
